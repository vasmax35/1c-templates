
#Область ПрограммныйИнтерфейс

// Обработка ошибок с возможностью отправки электронных писем в службу технической поддержки
// 
// Параметры:
//  ИнформацияОбОшибке - ИнформацияОбОшибке - Структурированная информация о возникшей ошибке
Процедура ОбработкаОшибок(ИнформацияОбОшибке) Экспорт
	
	Если ТипЗнч(ИнформацияОбОшибке) = Тип("Строка") Тогда
		ПодробныйТекстОшибки = ИнформацияОбОшибке;
	Иначе
		ПодробныйТекстОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
	КонецЕсли;
					
	ТекстВопроса1 = НСтр("ru = 'К сожалению, возникла непредвиденная ошибка.'");
	ТекстВопроса2 = ПодробныйТекстОшибки;
	ТекстВопроса3 = НСтр("ru = 'Перейти на форму формирования отчета об ошибке?'");
	
	ТекстВопроса = СтрШаблон("%1%4%4%2%4%4%3", ТекстВопроса1, ТекстВопроса2, ТекстВопроса3, Символы.ПС);
		
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИнформацияОбОшибке", ИнформацияОбОшибке);
	ДополнительныеПараметры.Вставить("ПодробныйТекстОшибки", ПодробныйТекстОшибки);
	
	ОповещениеПослеВопросаОФормированииОтчета = Новый ОписаниеОповещения("ПослеВопросаОФормированииОтчетаОбОшибке", ЭтотОбъект, ДополнительныеПараметры);
	ПоказатьВопрос(ОповещениеПослеВопросаОФормированииОтчета, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПослеВопросаОФормированииОтчетаОбОшибке(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = КодВозвратаДиалога.Да Тогда
		
		ИнформацияОбОшибке = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеПараметры, "ИнформацияОбОшибке");
		ПодробныйТекстОшибки = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеПараметры, "ПодробныйТекстОшибки");
		
		Если ИнформацияОбОшибке = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		МассивНавигационныхСсылок = Новый Массив;
		
		АктивноеОкноПриложения = АктивноеОкно();
		Если АктивноеОкноПриложения <> Неопределено
			И Не (АктивноеОкноПриложения.Основное Или АктивноеОкноПриложения.НачальнаяСтраница) Тогда
			
			СсылкаНаОбъект = АктивноеОкноПриложения.ПолучитьНавигационнуюСсылку();
			Если ЗначениеЗаполнено(СсылкаНаОбъект) Тогда
				МассивНавигационныхСсылок.Добавить(СсылкаНаОбъект);
			КонецЕсли;
			
		КонецЕсли;
		
		// BSLLS:MissingTemporaryFileDeletion-off			
		// Удаление временного файла реализовано в общей форме "ФормаРегистрацииОшибки"
		ОтчетОбОшибкеАрхив = ПолучитьИмяВременногоФайла("zip");
		// BSLLS:MissingTemporaryFileDeletion-on
		ОтчетОбОшибке = Новый ОтчетОбОшибке(ИнформацияОбОшибке);
	    ОтчетОбОшибке.Записать(ОтчетОбОшибкеАрхив, Ложь);

		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("ПодробныйТекстОшибки", ПодробныйТекстОшибки);
		ПараметрыОткрытия.Вставить("ОтчетОбОшибкеАрхив", ОтчетОбОшибкеАрхив);
		
		Если МассивНавигационныхСсылок.Количество() Тогда
			ПараметрыОткрытия.Вставить("МассивНавигационныхСсылок", МассивНавигационныхСсылок);
		КонецЕсли;
		
		ОткрытьФорму("ОбщаяФорма.ФормаРегистрацииОшибки", ПараметрыОткрытия,, Новый УникальныйИдентификатор);
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти
