#Область СлужебныйПрограммныйИнтерфейс

// Заполнить массив значений по выделенным строкам таблиц.
// 
// Параметры:
//  Форма - Форма - Форма элемента таблицы которой будут заполнены.
//  ИмяРеквизитаТаблицы - Строка - Имя таблицы значения которой должны быть сохранены.
//  ИмяРеквизитаСсылки - Строка - Имя реквизита переданной таблицы значения которого нужно сохранить в массив.
//  МассивИдентификаторов - Массив - Массив идентификаторов строк.
// 
// Возвращаемое значение:
//  Массив - Массив - массив значений по выделенным строкам таблиц.
Функция ЗаполнитьМассивЗначенийПоВыделеннымСтрокамТаблиц(Форма,
														 ИмяРеквизитаТаблицы,
														 ИмяРеквизитаСсылки,
														 МассивИдентификаторов) Экспорт
	МассивЗначений = Новый Массив;

	Для Каждого Идентификатор Из МассивИдентификаторов Цикл
		Если ТипЗнч(Форма[ИмяРеквизитаТаблицы]) = Тип("ДанныеФормыКоллекция")
		 ИЛИ ТипЗнч(Форма[ИмяРеквизитаТаблицы]) = Тип("ДанныеФормыДерево") Тогда
			СтрокаКоллекции = Форма[ИмяРеквизитаТаблицы].НайтиПоИдентификатору(Идентификатор);
			Если СтрокаКоллекции = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			МассивЗначений.Добавить(СтрокаКоллекции[ИмяРеквизитаСсылки]);
		ИначеЕсли ТипЗнч(Форма[ИмяРеквизитаТаблицы]) = Тип("ДинамическийСписок") Тогда
			Если Не ОбщегоНазначения.ЭтоСсылка(ТипЗнч(Идентификатор)) Тогда
				МассивЗначений.Добавить(Идентификатор[ИмяРеквизитаСсылки]);
			Иначе
				МассивЗначений.Добавить(Идентификатор);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

	Возврат МассивЗначений;
КонецФункции

#КонецОбласти
